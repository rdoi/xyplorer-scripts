::/** XYplorer: Add media CRC

Adds a CRC32 in the selected file's name.
At the end, it shows the execution log.

@author rdoi
@created apr/2014
*/
  $log="MediaCRCAppender: RDoi File CRC32 appender<crlf>-------------------------------<crlf>";
  foreach($item, "<get SelectedItemsPathNames>", "<crlf>") {
    if (exists($item) == 1) {
      $fname= regexreplace($item,"^.*\\", "");
      $crc= hash("crc32",$item,3);
    	if ( regexmatches($fname,"\[[0-9a-f]{8}\]") == "" ) {
          $r=renameitem("*.[$crc]",$item,1,"-01");
          if (strlen($r) > 0) {
            $log= $log."+RENAMED $r<crlf>";
          }
    	} else {
          if ( regexmatches($fname,"\[$crc\]") == "" ) {
            $log= $log."-CRC.ERROR $crc: ".$item."<crlf>";
          } else {
            $log= $log."=CRC.MATCH $crc: ".$item."<crlf>";
          }
    	}
    }
  }
  text $log;

